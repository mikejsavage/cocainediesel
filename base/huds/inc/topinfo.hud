setCursor #WIDTH / 2, 4
setAlignment center top
setFontBorder on

if %MATCH_STATE < #MATCH_STATE_PLAYTIME
	// background
	setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 32
	if %TEAM != #TEAM_SPECTATOR
		setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 48
	endif

	moveCursor 0, 15
	setFontSize %VIDHEIGHT / 50
	setFontStyle bold
	setColor 1, 1, 1, 1
	drawString "WARMUP"

	if %TEAM != #TEAM_SPECTATOR
		moveCursor 0, 17

		if %READY || %MATCH_STATE == #MATCH_STATE_COUNTDOWN
			setColor 0.32, 0.99, 0.37, 1
			drawString "READY"
		endif

		ifnot %READY || %MATCH_STATE == #MATCH_STATE_COUNTDOWN
			setAttentionGettingColor
			drawBindString "[%s] TO READY UP" "toggleready"
		endif
	endif

	setFontStyle normal
endif

if %MATCH_STATE == #MATCH_STATE_PLAYTIME
	// background
	setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 32
	if %ROUND_TYPE != #RoundType_Normal
		setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 48
	endif

	// clock
	setColor 1, 1, 1, 1
	setFontSize %VIDHEIGHT / 24
	moveCursor 0, 3
	drawClock

	if %TEAMBASED
		// scores
		setFontStyle bold
		moveCursor -50 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColorToTeamColor #TEAM_ALPHA
		drawStringNum %ALPHA_SCORE

		moveCursor 100 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColorToTeamColor #TEAM_BETA
		drawStringNum %BETA_SCORE

		moveCursor -50 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		// player icons
		setCursor #WIDTH / 2, 7

		setAlignment right top
		moveCursor -90 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColor 0.2 0.2 0.2 1
		drawPlayerIcons #TEAM_ALPHA, %ALPHA_PLAYERS_ALIVE, %ALPHA_PLAYERS_TOTAL

		moveCursor 180 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setAlignment left top

		setColor 0.2 0.2 0.2 1
		drawPlayerIcons #TEAM_BETA, %BETA_PLAYERS_ALIVE, %BETA_PLAYERS_TOTAL

		moveCursor -90 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	// match point/overtime
	if %ROUND_TYPE != #RoundType_Normal
		setAlignment center top
		moveCursor 0, 26
		setFontSize %VIDHEIGHT / 50
		setFontStyle bold
		setColor 1, 1, 1, 1
		if %ROUND_TYPE == #RoundType_MatchPoint
			drawString "MATCH POINT"
		endif
		if %ROUND_TYPE == #RoundType_Overtime
			drawString "OVERTIME"
		endif
		if %ROUND_TYPE == #RoundType_OvertimeMatchPoint
			drawString "OVERTIME MATCH POINT"
		endif
	endif
endif

setFontBorder off
