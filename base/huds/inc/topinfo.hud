setCursor #WIDTH / 2, 4
setAlignment center top
setFontBorder on

if %MATCH_STATE < #MATCH_STATE_PLAYTIME
	// background
	setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 32
	if %TEAM != #TEAM_SPECTATOR
		setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 48
	endif

	moveCursor 0, 15
	setFontSize %VIDHEIGHT / 50
	setFontStyle bold
	setColor 1, 1, 1, 1
	drawString "WARMUP"

	if %TEAM != #TEAM_SPECTATOR
		moveCursor 0, 17
		setFontSize %VIDHEIGHT / 50
		setFontStyle bold

		if %READY || %MATCH_STATE == #MATCH_STATE_COUNTDOWN
			setColor 0.32, 0.99, 0.37, 1
			drawString "READY"
		endif

		ifnot %READY || %MATCH_STATE == #MATCH_STATE_COUNTDOWN
			setColor 1, 0.23, 0.26, 1
			drawString "NOT READY"
		endif
	endif

	setFontStyle normal
endif

if %MATCH_STATE == #MATCH_STATE_PLAYTIME
	// background
	setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 32
	if %ROUND_TYPE != #RoundType_Normal
		setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 48
	endif

	// clock
	setColor 1, 1, 1, 1
	setFontSize %VIDHEIGHT / 24
	moveCursor 0, 3
	drawClock

	// scores
	setFontStyle bold

	if %TEAM_ALPHA_SCORE != #NOTSET
		moveCursor -50 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColorToTeamColor #TEAM_ALPHA
		drawStringNum %TEAM_ALPHA_SCORE

		moveCursor 50 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	if %TEAM_BETA_SCORE != #NOTSET
		moveCursor 50 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColorToTeamColor #TEAM_BETA
		drawStringNum %TEAM_BETA_SCORE

		moveCursor -50 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	// player icons
	setCursor #WIDTH / 2, 7

	if %ALPHA_PLAYERS_TOTAL
		setAlignment right top
		moveCursor -90 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColor 0.2 0.2 0.2 1
		drawPlayerIcons #TEAM_ALPHA, %ALPHA_PLAYERS_ALIVE, %ALPHA_PLAYERS_TOTAL

		moveCursor 90 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	if %BETA_PLAYERS_TOTAL
		setAlignment left top
		moveCursor 90 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColor 0.2 0.2 0.2 1
		drawPlayerIcons #TEAM_BETA, %BETA_PLAYERS_ALIVE, %BETA_PLAYERS_TOTAL

		moveCursor -90 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	// match point/overtime
	if %ROUND_TYPE != #RoundType_Normal
		moveCursor 0, 26
		setFontSize %VIDHEIGHT / 50
		setFontStyle bold
		setColor 1, 1, 1, 1
		if %ROUND_TYPE == #RoundType_MatchPoint
			drawString "MATCH POINT"
		endif
		if %ROUND_TYPE == #RoundType_Overtime
			drawString "OVERTIME"
		endif
		if %ROUND_TYPE == #RoundType_OvertimeMatchPoint
			drawString "OVERTIME MATCH POINT"
		endif
	endif
endif

setFontBorder off
